apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'

def yandexApiKey = hasProperty('YANDEX_RASP_API_KEY') ? YANDEX_RASP_API_KEY : ""

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.pengrad.raspkursk"
        minSdkVersion 11
        targetSdkVersion 29
        versionCode 3
        versionName "1.1"

        buildConfigField "String", "YANDEX_API_KEY", "\"" + yandexApiKey + "\""

        testInstrumentationRunner "com.pengrad.raspkursk.TestRunner"
    }

    signingConfigs {
        release {
            storeFile file(hasProperty('ANDROID_KEYSTORE') ? ANDROID_KEYSTORE : "/dev/null")
            storePassword hasProperty('ANDROID_KEYSTORE_PASSWORD') ? ANDROID_KEYSTORE_PASSWORD : ""
            keyAlias hasProperty('ANDROID_KEYSTORE_KEY') ? ANDROID_KEYSTORE_KEY : ""
            keyPassword hasProperty('ANDROID_KEYSTORE_KEY_PASSWORD') ? ANDROID_KEYSTORE_KEY_PASSWORD : ""
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
        checkAllWarnings true
    }

    play {
        jsonFile = file(hasProperty('GOOGLE_PLAY_KEY_FILE') ? GOOGLE_PLAY_KEY_FILE : "/dev/null")
        track = 'alpha'
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:24.2.1'
    implementation 'com.android.support:design:24.2.1'
    implementation 'com.android.support:recyclerview-v7:24.2.1'
    implementation 'com.android.support:cardview-v7:24.2.1'

    implementation 'com.squareup.retrofit:retrofit:1.9.0'
    implementation 'io.reactivex:rxandroid:0.25.0'
    implementation 'io.reactivex:rxjava:1.0.12'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:2.2.2'
}
